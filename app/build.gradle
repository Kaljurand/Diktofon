apply plugin: 'com.android.application'

dependencies {
    implementation fileTree(dir: 'libs', include: '*.jar')
    implementation project(':net-speech-api')
    implementation 'commons-io:commons-io:2.5'
    implementation 'com.googlecode.json-simple:json-simple:1.1'
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId 'kaljurand_at_gmail_dot_com.diktofon'
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1103
        versionName '1.1.03'
    }

    // TODO: try to remove it
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE_DIKTOFON"))
            keyAlias System.getenv("KEYSTORE_DIKTOFON_ALIAS")
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard.cfg'
            signingConfig signingConfigs.release
        }
    }
}


if (project.hasProperty('storePassword') &&
    project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}

task(deploy) << {
    description 'Copy the APK and the ProGuard mapping file to the deploy directory'

    def deploy_dir = System.getenv('APK_DEPLOY_DIR')

    def version = android.defaultConfig.versionName

    def outputs = 'build/outputs/'
    def apk1 = outputs + 'apk/release/app-release.apk'
    def apk2 = "${deploy_dir}Diktofon-${version}.apk"
    def mapping1 = outputs + 'mapping/release/mapping.txt'
    def mapping2 = "${deploy_dir}Diktofon-mapping-${version}.txt"

    exec {
        commandLine 'cp', '--verbose', apk1, apk2
    }

    exec {
        commandLine 'cp', '--verbose', mapping1, mapping2
    }

    exec {
        commandLine 'ls', '-l', deploy_dir
    }

    println "adb uninstall kaljurand_at_gmail_dot_com.diktofon"
    println "adb install ${apk2}"
}
